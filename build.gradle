buildscript {
	repositories {
		mavenCentral()
		maven {
			name 'forge'
			url 'https://maven.minecraftforge.net'
		}
	}
	dependencies {
		classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
			changing = true
		}
	}
}

repositories {
	flatDir {
		dirs 'lib'
	}
}

apply plugin: 'forge'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

version = "1.7.10-1.13.2"
group= "com.gamerforea"
archivesBaseName = "EventHelper"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "1.7.10-10.13.4.1614-1.7.10"
	runDir = "eclipse"
	replace "@VERSION@", project.version
}

dependencies {
	implementation name: 'bukkit-1.7.10'
	implementation name: 'worldedit-6.1'
	implementation name: 'worldguard-6.1'
	implementation name: 'GriefPreventionPlus-13.3'
}

task devJar(type: Jar) {
	classifier = 'dev'
	from sourceSets.main.output
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives devJar
	archives sourcesJar
}